buildscript {
    ext {
        jetbrainsAnnotationsVersion = "13.0"
        junitVersion = "4.12"
        assertJVersion = "3.4.1"
        mockitoVersion = "1.10.19"
    }
    repositories {
        mavenCentral()
    }
}

group = "me.ccampo"
version = "1.0.0"

apply plugin: "java"
apply plugin: "maven"
apply plugin: "jacoco"
apply plugin: "signing"

jacoco {
    toolVersion = "0.7.5.201505241946"
}

jar {
    baseName = "exponential-backoff"
    archivesBaseName = "exponential-backoff"
    manifest {
        attributes("Implementation-Title": "A small library to retry arbitrary tasks with exponential backoff (with " +
                "jitter).")
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains:annotations:${jetbrainsAnnotationsVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.assertj:assertj-core:${assertJVersion}"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.13"
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required = false
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name "Exponential Backoff"
                packaging "jar"
                description "A small library to retry arbitrary tasks with exponential backoff (with jitter)."
                url "https://github.com/ccampo133/exponential-backoff"

                scm {
                    connection "scm:git:https://github.com/ccampo133/exponential-backoff.git"
                    developerConnection "scm:git:https://github.com/ccampo133/exponential-backoff.git"
                    url "https://github.com/ccampo133/exponential-backoff.git"
                }

                licenses {
                    license {
                        name "The MIT License (MIT)"
                        url "https://raw.githubusercontent.com/ccampo133/exponential-backoff/master/LICENSE"
                    }
                }

                developers {
                    developer {
                        id "ccampo133"
                        name "Christopher Campo"
                        email "ccampo.progs@gmail.com"
                    }
                }
            }
        }
    }
}

